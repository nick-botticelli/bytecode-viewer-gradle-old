plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group 'the.bytecode.club'
version '2.9.22.1-SNAPSHOT'

sourceCompatibility = 1.11
targetCompatibility = 1.11

defaultTasks 'shadowJar'

repositories {
    mavenCentral()
    google()
    maven { url 'https://jitpack.io' }
    flatDir { dirs 'libs' }
}

jar {
    manifest {
        attributes 'Main-Class': 'the.bytecode.club.bytecodeviewer.BytecodeViewer'
    }
}

shadowJar {
    archiveClassifier.set('shaded')

    minimize() {
        // TODO: Uses RSyntaxTextArea causing conflict
        exclude(dependency('com.fifesoft:.*:.*'))
        exclude(dependency(':jd-gui-1.4.0:.*:.*'))
    }
}

// Create reproducible archive
tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

dependencies {
    implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.2.3'
    implementation group: 'com.fifesoft', name: 'rsyntaxtextarea', version: '3.1.2'
    implementation group: 'com.github.fesh0r', name:'fernflower', version: '-SNAPSHOT' // Latest Fernflower
    implementation group: 'com.github.vlsi.mxgraph', name: 'jgraphx', version: '4.2.2'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    implementation group: 'net.oneandone', name: 'jasmin', version: '3.0.13'
    // TODO: Update to newest version (runtime bugs/errors in 1.2.0)
    implementation group: 'com.github.skylot.jadx', name: 'jadx-core', version: 'v1.2.0'
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.15'
    implementation group: 'commons-io', name: 'commons-io', version: '2.8.0'
    implementation group: 'org.apache.commons', name: 'commons-compress', version: '1.20'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.9'
    implementation group: 'org.benf', name: 'cfr', version: '0.151'
    implementation group: 'org.codehaus.janino', name: 'janino', version: '3.1.4'
    implementation group: 'org.imgscalr', name: 'imgscalr-lib', version: '4.2'
    implementation group: 'org.ow2.asm', name: 'asm', version: '9.1'
    implementation group: 'org.ow2.asm', name: 'asm-commons', version: '9.1'
    implementation group: 'org.ow2.asm', name: 'asm-tree', version: '9.1'
    implementation group: 'org.ow2.asm', name: 'asm-util', version: '9.1'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    implementation group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.30'
    implementation group: 'org.smali', name: 'baksmali', version: '2.5.2'

    implementation name: 'android-29-clst'
    implementation name: 'apktool-cli-all-2.5.1-dev'
    implementation name: 'byteanalysis-1.0'
    implementation name: 'd2j-base-cmd-2.1-SNAPSHOT'
    implementation name: 'd2j-smali-2.1-SNAPSHOT'
    implementation name: 'dex-ir-2.1-SNAPSHOT'
    implementation name: 'dex-reader-2.1-SNAPSHOT'
    implementation name: 'dex-reader-api-2.1-SNAPSHOT'
    implementation name: 'dex-tools-2.1-SNAPSHOT'
    implementation name: 'dex-translator-2.1-SNAPSHOT'
    implementation name: 'dex-writer-2.1-SNAPSHOT'
    implementation name: 'dx-31.0.0-rc2' // Get from Android SDK Build-Tools
    implementation name: 'jar-rename-1.6'
    implementation name: 'jd-gui-1.4.0' // TODO: Update to newest version
    implementation name: 'procyon-compilertools-1.0-SNAPSHOT'
    implementation name: 'procyon-core-1.0-SNAPSHOT'
}